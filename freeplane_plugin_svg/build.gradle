dependencies {
    compile project(':freeplane')

    compile 'org.apache.xmlgraphics:batik-anim:1.7',
            'org.apache.xmlgraphics:batik-awt-util:1.7',
            'org.apache.xmlgraphics:batik-bridge:1.7',
            'org.apache.xmlgraphics:batik-codec:1.7',
            'org.apache.xmlgraphics:batik-css:1.7',
            'org.apache.xmlgraphics:batik-dom:1.7',
            'org.apache.xmlgraphics:batik-extension:1.7',
            'org.apache.xmlgraphics:batik-ext:1.7',
            'org.apache.xmlgraphics:batik-gui-util:1.7',
            'org.apache.xmlgraphics:batik-gvt:1.7',
            'org.apache.xmlgraphics:batik-parser:1.7',
            'org.apache.xmlgraphics:batik-script:1.7',
            'org.apache.xmlgraphics:batik-svg-dom:1.7',
            'org.apache.xmlgraphics:batik-svggen:1.7',
            'org.apache.xmlgraphics:batik-swing:1.7',
            'org.apache.xmlgraphics:batik-transcoder:1.7',
            'org.apache.xmlgraphics:batik-util:1.7',
            'org.apache.xmlgraphics:batik-xml:1.7',

            'org.mozilla:rhino:1.7R4', // "js.jar"/librhino-java

            // cannot pull this due to broken "avalon" dependency
            // (replaced by ./lib/pdf-transcoder.jar below):
            //'org.apache.xmlgraphics:fop:1.1', // "fop.jar"/libfop-java

            // libxml-commons-external-java 1.4.01
            //WRONG: 'org.apache.xmlgraphics:xmlgraphics-commons:1.4'
            'xml-apis:xml-apis:1.4.01',
            'xml-apis:xml-apis-ext:1.3.04',

            // need to fix javax.xml.parsers.FactoryConfigurationError:
            // Provider org.apache.xerces.jaxp.SAXParserFactoryImpl not found
            // when starting from elipse:
            'xerces:xercesImpl:2.11.0'

    // from flatDir "localGitDepsRepository":
    compile ':pdf-transcoder-1.0' // "fop.jar"/libfop-java
}

jar {
    manifest {
        name = pluginid
        symbolicName = pluginid 

        configurations.runtime.transitive = false
        def deps = filterAndExternalizePluginDependencies(configurations.runtime.files)
        deps.add(0, "lib/plugin-" + project.version + '.jar')

        attributes 'Class-Path': deps.join(', ')
        attributes 'Bundle-ClassPath': '., ' + deps.join(', ')

        instruction 'Bundle-Vendor', 'Freeplane Project'
        instruction 'Bundle-Activator', pluginid + ".Activator"
        instruction 'Bundle-RequiredExecutionEnvironment', 'JavaSE-1.7'

        def imports = """\
                      org.osgi.framework;version="1.3.0",
                      org.freeplane.core.*,
                      org.freeplane.features.*,
                      org.freeplane.main.*,
                      org.freeplane.n3.*,
                      org.freeplane.view.*
                      """
        instruction 'Import-Package', imports.replaceAll("\\s+", "")
        instruction 'Export-Package', ''

        // Require-Bundle is ignored by bnd => replaced by Import-Package!
        //instruction 'Require-Bundle', 'org.freeplane.core;bundle-version="1.0.1", org.freeplane.plugin.script;bundle-version="1.0.1"'
    }
}
